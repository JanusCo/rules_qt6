load("@de_vertexwahn_rules_qt6//:qt_libraries.bzl", "QT_LIBRARIES")

[
    cc_library(
        name = "qt_%s" % name,
        visibility = ["//visibility:public"],
        deps = dependencies + select({
            "@platforms//os:linux": ["@qt_6.2.4_linux_desktop_gcc_64//:qt_%s_linux" % name],
            "@bazel_tools//src/conditions:darwin_x86_64": ["@qt_6.2.4_mac_desktop_clang_64//:qt_%s_mac" % name],
            "@bazel_tools//src/conditions:darwin_arm64": ["@qt_6.2.4_mac_desktop_clang_64_M1//:qt_%s_mac" % name],
            "@platforms//os:windows": ["@qt_6.2.4_windows_desktop_win64_msvc2019_64//:qt_%s_windows" % name],
        }),
    )
    for name, _, _, dependencies in QT_LIBRARIES
]

cc_binary(
    name = "Qt6HelloWorld",
    srcs = ["main.cpp"],
    deps = [
        ":qt_core",
        ":qt_qml",
        ":qt_widgets",
    ],
    env = select({
        "@platforms//os:linux": {
            "QT_QPA_PLATFORM_PLUGIN_PATH": "external/qt_6.2.4_linux_desktop_gcc_64/plugins",
        },
        "@bazel_tools//src/conditions:darwin_x86_64": {
            "QT_QPA_PLATFORM_PLUGIN_PATH": "external/qt_6.2.4_mac_desktop_clang_64/share/qt/plugins",
        },
        "@bazel_tools//src/conditions:darwin_arm64": {
            "QT_QPA_PLATFORM_PLUGIN_PATH": "external/qt_6.2.4_mac_desktop_clang_64_M1/share/qt/plugins",
        },
        "@platforms//os:windows": {
            "QT_QPA_PLATFORM_PLUGIN_PATH": "external/qt_6.2.4_windows_desktop_win64_msvc2019_64/plugins",
        },
    }),
    data = select({
        "@platforms//os:linux": ["@qt_6.2.4_linux_desktop_gcc_64//:plugin_files"],
        "@bazel_tools//src/conditions:darwin_x86_64": ["@qt_6.2.4_mac_desktop_clang_64//:plugin_files"],
        "@bazel_tools//src/conditions:darwin_arm64": ["@qt_6.2.4_mac_desktop_clang_64_M1//:plugin_files"],
        "@platforms//os:windows": ["@qt_6.2.4_windows_desktop_win64_msvc2019_64//:plugin_files"],
    }),
)
